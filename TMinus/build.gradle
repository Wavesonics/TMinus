buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    // Google Play Services
    compile 'com.google.android.gms:play-services:4.2.+'

    // Support Libraries
    compile 'com.android.support:support-v4:19.0.1'
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.android.support:gridlayout-v7:19.0.1'
    compile 'com.android.support:support-v13:19.0.1'

    compile 'com.google.code.gson:gson:2.2.4'
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.2'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:library:0.9.3'
    compile 'com.nhaarman.listviewanimations:library:2.5.2'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'joda-time:joda-time:2.3'

    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'

    compile files('libs/volley.jar')
    compile files('libs/CWAC-WakefulIntentService.jar')
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {

        }

        debug {

        }
    }

    buildTypes {
        release {
            debuggable false
            zipAlign true
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            zipAlign false
        }
    }

    // Read our private signing details from the local properties file
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPassword')) {
        println 'gradle.properties OK'
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        println 'gradle.properties not well formed'
        if (!project.hasProperty('storeFile')) println 'storeFile not defined'
        if (!project.hasProperty('storePassword')) println 'storePassword not defined'
        if (!project.hasProperty('keyAlias')) println 'keyAlias not defined'
        if (!project.hasProperty('keyPassword')) println 'keyPassword not defined'

        buildTypes.release.signingConfig = null
    }
}

